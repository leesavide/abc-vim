% bracinho.abc        -*- abc -*-
%    Sample file for `bracinho.fmt'
%    Arquivo de exemplo para `bracinho.fmt'
% Hudson Lacerda (2004)

%%format bracinho

%%pageheight  29.7cm
%%pagewidth   21.0cm
%%topmargin    1.5cm
%%botmargin    1.5cm
%%leftmargin   1.5cm
%%rightmargin  1.5cm
%%topspace     0
%%musicspace   0
%%continueall  true

% %textoption justify
%%textfont Times-Roman 16
%%setfont-1 Courier-Bold 16
%%setfont-2 Times-Bold 20

%%center $2bracinho.abc - Exemplo de bracinho.fmt
%%center Hudson Lacerda (2004)
%%text

%%begintext obeylines
% SINTAXE:
% 
% deco <nome> 3 bracinho 50 0 0 <casa>^<nome> <descr(1)> <descr(2)> <descr(3)> ...
% 
% <casa>^<nome>
% A primeira parte da string é impressa à direita do bracinho, na altura
% da primeira casa, e serve para indicar a posição (casa).  Quando um
% caracter ^ é encontrado, o restante da string é impresso acima do
% bracinho (nome do acorde).
% - Se essa string começa com um hífen, é ignorada.
% - Um espaço poder ser representado por \\1 (ou \\20 etc. mas não por \\0 ou \\040)
%
% Para ocultar/exibir o nome, execute um operador postscript:
% %%postscript bracinho_com_nome
% %%postscript bracinho_sem_nome
% 
% <descr(N)>
% As strings subseqüentes descrevem a ação sobre cada corda:
% 
% - Um algarismo de 1 a 5 indica a casa em que deve-se colocar um dedo
% (bolinha). 0 indica corda solta.
% - Um x coloca indicação de não tocar a corda.
% - [ após um algarismo inicia uma pestana.
% - ] após um algarismo termina uma pestana.
% - { após um algarismo inicia uma pestana.
% - } após um algarismo termina uma pestana.
% 
% As pestanas indicadas por colchetes são independentes daquelas
% indicadas por chaves.
% 
% - Qualquer outro caracter é ignorado.
% (Sugere-se utilizar hífen - para `pular' ou adicionar uma corda sem
% símbolos.)
% 
% O número de cordas é calculado automaticamente como o número de
% <descr(N)> utilizados. O número de casas é fixo em 5.
%
% O operador bracinho_grande gera um bracinho 50% maior que o normal.
%
% Exemplo:
%
%    %%postscript (^Am x 0 2 2 1 0) 0 0 bracinho_grande
%%endtext

%%newpage

%%begintext
%
% Exemplos:
% 

- Violão/Guitarra:

%%endtext


%%text Básico:
%%text $1\%\%deco C  3 bracinho 50 0 0   ^C    x 3 2 0 1 0
%%text $1\%\%deco G7 3 bracinho 50 0 0   ^G7   3 2 0 0 0 1
%%text $1X:1
%%text $1K:C
%%text $1!C!cdeg - !G7!g4 |]

%%deco C  3 bracinho 50 0 0 ^C  x 3 2 0 1 0
%%deco G7 3 bracinho 50 0 0 ^G7 3 2 0 0 0 1
X:1
K:C
!C!cdeg - !G7!g4 |]



%%text Com nome (^Am9/G):
%%text $1\%\%deco Am9/G 3 bracinho 50 0 0  ^Am9/G    3 0 2 4 1 0
%%text Sem nome, mas com indicação de posição (1a. casa):
%%text $1\%\%deco Ab/C  3 bracinho 50 0 0  1a.\\1casa x 3 1 1 4 1
%
%%deco Ab/C 3 bracinho 50 0 0 1a.\1casa x 3 1 1 4 1
%%deco Am9/G 3 bracinho 50 0 0 ^Am9/G 3 0 2 4 1 0
%

X:1
K:C
!Am9/G!CDEG !Ab/C!_Ac3 |

%%text Várias bolinhas na mesma casa (algarismos sem espaços):
%%text $1\%\%deco escala 3 bracinho 50 0 0 2a.\\1casa\\1(escala) x 24 124 134 x x

%%deco escala 3 bracinho 50 0 0   2a.\1casa\1(escala) x 24 124 134 x x
X:1
K:C
!escala! C D E F G A B c |


%%text Bracinhos com pestanas (colchetes e chaves):
%%begintext
$1\%\%deco G#m  3 bracinho 50 0 0     IV  1[  3   3   -   -   1]
$1\%\%deco A/C# 3 bracinho 50 0 0     -   x   4   2[  -   5{  2]5}
$1\%\%deco A/C#(II) 3 bracinho 50 0 0 II  x   3   1[  1   4{  1]4}
%%endtext

%%deco G#m  3 bracinho 50 0 0     IV  1[  3   3   -   -   1]
%%deco A/C# 3 bracinho 50 0 0     -   x   4   2[  -   5{  2]5}
%%deco A/C#(II) 3 bracinho 50 0 0 II  x   3   1[  1   4{  1]4}
X:1
K:E
A,B,CG - | !G#m!GD3 !A/C#!A,B,CG | !A/C#(II)!E8 |

%%newpage

%%text Número de cordas variado:
%%text - Cavaquinho:
%%begintext
$1\%\%deco Em 3 bracinho 50 0 0 ^Em   2 0 0 2  \% 4 cordas
%%endtext

%%deco Em 3 bracinho 50 0 0 ^Em 2 0 0 2     % 4 cordas

X:1
K:Em
!Em![EGBe]8 |


%%text - Viola caipira (cebolão):
%%begintext
$1\%\%deco B7 3 bracinho 50 0 0 ^B7  0 2 3 4 5  \% 5 cordas
%%endtext

%%deco B7 3 bracinho 50 0 0 ^B7 0 2 3 4 5   % 5 cordas
X:1
K:E
!B7![B,FBda]8 |

